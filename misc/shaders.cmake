function (ngf_shaders_target)
   cmake_parse_arguments(SHADERS_TARGET "" "NAME;OUTPUT_DIR;NICESHADE_PATH" "SRCS" ${ARGN})
   foreach(source_path ${SHADERS_TARGET_SRCS})
      file(STRINGS ${source_path} tech_lines REGEX "// *T *: *([a-zA-Z0-9_]+)")
      if (tech_lines)
        set(tech_names "")
        foreach(tech_line ${tech_lines})
          string(REPLACE ":" ";" tmp ${tech_line})
          list(GET tmp 1 tmp)
          string(STRIP "${tmp}" tmp)
          string(REGEX REPLACE " +" ";" tmp ${tmp})
          list(GET tmp 0 tech_name)
          list(APPEND tech_names "${tech_name}")
        endforeach(tech_line)
        set(output_files_list "")
        get_filename_component(header_file_name ${source_path} NAME_WE)
        if(NOT ${header_file_name} MATCHES "compute-*")
          foreach(tech ${tech_names})
            list(APPEND output_files_list "${SHADERS_TARGET_OUTPUT_DIR}/${tech}.vs.21.msl")
            list(APPEND output_files_list "${SHADERS_TARGET_OUTPUT_DIR}/${tech}.ps.21.msl")
            list(APPEND output_files_list "${SHADERS_TARGET_OUTPUT_DIR}/${tech}.vs.spv")
            list(APPEND output_files_list "${SHADERS_TARGET_OUTPUT_DIR}/${tech}.ps.spv")
            list(APPEND output_files_list "${SHADERS_TARGET_OUTPUT_DIR}/${tech}.pipeline")
          endforeach(tech)
        else()
          foreach(tech ${tech_names})
            list(APPEND output_files_list "${SHADERS_TARGET_OUTPUT_DIR}/${tech}.cs.21.msl")
            list(APPEND output_files_list "${SHADERS_TARGET_OUTPUT_DIR}/${tech}.cs.spv")
            list(APPEND output_files_list "${SHADERS_TARGET_OUTPUT_DIR}/${tech}.pipeline")
          endforeach(tech)
        endif()
        list(APPEND output_files_list "${SHADERS_TARGET_OUTPUT_DIR}/${header_file_name}_binding_consts.h")
        add_custom_command(OUTPUT ${output_files_list}
                           MAIN_DEPENDENCY ${source_path}
                           COMMAND ${SHADERS_TARGET_NICESHADE_PATH}/niceshade ARGS ${source_path} "-t" "msl21" "-t" "spv" "-O" "${SHADERS_TARGET_OUTPUT_DIR}" "-h" "${header_file_name}_binding_consts.h")
                           #WORKING_DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/samples/shaders)
        set(generated_shaders_list "${output_files_list};${generated_shaders_list}")
      endif()
    endforeach(source_path)
    add_custom_target(${SHADERS_TARGET_NAME} DEPENDS ${generated_shaders_list})
endfunction()